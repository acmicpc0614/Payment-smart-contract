
// File: contracts/Config.sol

pragma solidity >=0.5.12 <0.6.0;

/*
    Stores config data of forwarder so it will be same for all proxies.
*/
contract Config {
    address public owner;

    function getConfig(bytes32 slot) public view returns (bytes32 res) {
        assembly {
            res := sload(slot)
        }
    }

    function getAddress(bytes32 slot) public view returns(address res)  {
        assembly {
            res := sload(slot)
        }
    }

    function getUint(bytes32 slot) public view returns(uint256 res) {
        assembly {
            res := sload(slot)
        }
    }

    function addConfig(bytes32 slot, bytes32 value) public {
        require (msg.sender == owner);
        require (slot != 0x0);  // Don't allow to set owner using addConfig function to avoid not intendet behaviour.
        assembly {
            sstore(slot, value)
        }
    }

    /* Function allows to set new owner.
       During first call (when owner is not yet set) anyone can set it. Later only current owner can set new one.
     */
    function setOwner(address _owner) public {
        require (msg.sender == owner || owner == address(0x0));
        owner = _owner;
    }
}

// File: contracts/ChannelIImplementationProxy.sol

pragma solidity >=0.5.12 <0.6.0;


contract ChannelImplementationProxy {
    address constant CONFIG_ADDRESS = 0xF8B0E425AB9BE026B67a6429F0C8E3394983EdA8;

    function () external payable {
        address _target = Config(CONFIG_ADDRESS).getAddress(0x48df65c92c1c0e8e19a219c69bfeb4cf7c1c123e0c266d555abb508d37c6d96e); // keccak256('channel implementation')
        assembly {
            let ptr := mload(0x40)
            calldatacopy(ptr, 0, calldatasize)
            let success := delegatecall(sub(gas, 10000), _target, ptr, calldatasize, 0, 0)
            let retSz := returndatasize
            returndatacopy(ptr, 0, retSz)

            switch success
            case 0 { revert(ptr, retSz) }
            default { return(ptr, retSz) }
        }
    }
}
