
// File: openzeppelin-solidity/contracts/cryptography/ECDSA.sol

pragma solidity ^0.5.0;

/**
 * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.
 *
 * These functions can be used to verify that a message was signed by the holder
 * of the private keys of a given address.
 */
library ECDSA {
    /**
     * @dev Returns the address that signed a hashed message (`hash`) with
     * `signature`. This address can then be used for verification purposes.
     *
     * The `ecrecover` EVM opcode allows for malleable (non-unique) signatures:
     * this function rejects them by requiring the `s` value to be in the lower
     * half order, and the `v` value to be either 27 or 28.
     *
     * (.note) This call _does not revert_ if the signature is invalid, or
     * if the signer is otherwise unable to be retrieved. In those scenarios,
     * the zero address is returned.
     *
     * (.warning) `hash` _must_ be the result of a hash operation for the
     * verification to be secure: it is possible to craft signatures that
     * recover to arbitrary addresses for non-hashed data. A safe way to ensure
     * this is by receiving a hash of the original message (which may otherwise)
     * be too long), and then calling `toEthSignedMessageHash` on it.
     */
    function recover(bytes32 hash, bytes memory signature) internal pure returns (address) {
        // Check the signature length
        if (signature.length != 65) {
            return (address(0));
        }

        // Divide the signature in r, s and v variables
        bytes32 r;
        bytes32 s;
        uint8 v;

        // ecrecover takes the signature parameters, and the only way to get them
        // currently is to use assembly.
        // solhint-disable-next-line no-inline-assembly
        assembly {
            r := mload(add(signature, 0x20))
            s := mload(add(signature, 0x40))
            v := byte(0, mload(add(signature, 0x60)))
        }

        // EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature
        // unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines
        // the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most
        // signatures from current libraries generate a unique signature with an s-value in the lower half order.
        //
        // If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value
        // with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or
        // vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept
        // these malleable signatures as well.
        if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
            return address(0);
        }

        if (v != 27 && v != 28) {
            return address(0);
        }

        // If the signature is valid (and not malleable), return the signer address
        return ecrecover(hash, v, r, s);
    }

    /**
     * @dev Returns an Ethereum Signed Message, created from a `hash`. This
     * replicates the behavior of the
     * [`eth_sign`](https://github.com/ethereum/wiki/wiki/JSON-RPC#eth_sign)
     * JSON-RPC method.
     *
     * See `recover`.
     */
    function toEthSignedMessageHash(bytes32 hash) internal pure returns (bytes32) {
        // 32 is the length in bytes of hash,
        // enforced by the type signature above
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }
}

// File: openzeppelin-solidity/contracts/math/SafeMath.sol

pragma solidity ^0.5.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers. Reverts on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0, "SafeMath: division by zero");
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * Reverts when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b != 0, "SafeMath: modulo by zero");
        return a % b;
    }
}

// File: openzeppelin-solidity/contracts/token/ERC20/IERC20.sol

pragma solidity ^0.5.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP. Does not include
 * the optional functions; to access them see `ERC20Detailed`.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a `Transfer` event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through `transferFrom`. This is
     * zero by default.
     *
     * This value changes when `approve` or `transferFrom` are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * > Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an `Approval` event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a `Transfer` event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to `approve`. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// File: openzeppelin-solidity/contracts/ownership/Ownable.sol

pragma solidity ^0.5.0;

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be aplied to your functions to restrict their use to
 * the owner.
 */
contract Ownable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () internal {
        _owner = msg.sender;
        emit OwnershipTransferred(address(0), _owner);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(isOwner(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Returns true if the caller is the current owner.
     */
    function isOwner() public view returns (bool) {
        return msg.sender == _owner;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * > Note: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public onlyOwner {
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     */
    function _transferOwnership(address newOwner) internal {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

// File: contracts/FundsRecovery.sol

pragma solidity ^0.5.12;



contract FundsRecovery is Ownable {
    address payable internal fundsDestination;
    IERC20 public token;

    event DestinationChanged(address indexed previousDestination, address indexed newDestination);

    /**
     * Setting new destination of funds recovery.
     */
    function setFundsDestination(address payable _newDestination) public onlyOwner {
        require(_newDestination != address(0));
        emit DestinationChanged(fundsDestination, _newDestination);
        fundsDestination = _newDestination;
    }

    /**
     * Getting funds destination address.
     */
    function getFundsDestination() public view returns (address) {
        return fundsDestination;
    }

    /**
     * Possibility to recover funds in case they were sent to this address before smart contract deployment
     */
    function claimEthers() public {
        require(fundsDestination != address(0));
        fundsDestination.transfer(address(this).balance);
    }

    /**
       Transfers selected tokens into owner address.
    */
    // TODO add reentrancy protection
    function claimTokens(address _token) public {
        require(fundsDestination != address(0));
        require(_token != address(token), "native token funds can't be recovered");
        uint256 _amount = IERC20(_token).balanceOf(address(this));
        IERC20(_token).transfer(fundsDestination, _amount);
    }
}

// File: contracts/AccountantImplementation.sol

pragma solidity ^0.5.12;





interface IdentityRegistry {
    function isRegistered(address _identityHash) external view returns (bool);
    function minimalAccountantStake() external view returns (uint256);
}

// Uni-directional settle based accountant
contract AccountantImplementation is FundsRecovery {
    using ECDSA for bytes32;
    using SafeMath for uint256;

    string constant LOAN_RETURN_PREFIX = "Load return request";
    uint256 constant DELAY_BLOCKS = 18000;  // +/- 3 days
    uint256 constant UNIT_BLOCKS = 257;     // 1 unit = 1 hour = 257 blocks. 

    IdentityRegistry internal registry;
    address internal operator;
    uint256 internal lockedFunds;              // funds locked in channels
    uint256 internal totalLoan;                // total amount lended by providers
    uint256 internal maxLoan;                  // maximal allowed provider's loan
    uint256 internal stake;                    // accountant stake is used to prove accountant's sustainability
    uint256 internal closingTimelock;          // blocknumber after which getting stake back will become possible

    enum Status { Active, Paused, Punishment, Closed } // accountant states
    Status internal status;

    struct AccountantFee {
        uint16 value;                      // subprocent amount. e.g. 2.5% = 250
        uint64 validFrom;                  // block from which fee is valid
    }
    AccountantFee public lastFee;          // default fee to look for
    AccountantFee public previousFee;      // previous fee is used if last fee is still not active

    struct Channel {
        address beneficiary;        // address where funds will be send
        uint256 balance;            // amount available to settle
        uint256 settled;            // total amount already settled by provider
        uint256 loan;               // amount lended by party to accountant
        uint256 lastUsedNonce;      // last known nonce, is used to protect signature based calls from repply attack
        uint256 timelock;           // blocknumber after which channel balance can be decreased
    }
    mapping(bytes32 => Channel) public channels;

    struct Punishment {
        uint256 activationBlock;    // block number in which punishment was activated
        uint256 amount;             // total amount of tokens locked because of punishment
    }
    Punishment public punishment;

    function getOperator() public view returns (address) {
        return operator;
    }

    function getChannelId(address _party) public view returns (bytes32) {
        return keccak256(abi.encodePacked(_party, address(this)));
    }

    function getRegistry() public view returns (address) {
        return address(registry);
    }

    function getStake() public view returns (uint256) {
        return stake;
    }

    // Returns accountant state
    // Active - all operations are allowed.
    // Paused - no new channel openings.
    // Punishment - don't allow to open new channels, rebalance and withdraw funds.
    // Closed - no new channels, no rebalance, no stake increase.
    function getStatus() public view returns (Status) {
        return status;
    }

    event ChannelOpened(bytes32 channelId, uint256 initialBalance);
    event ChannelBalanceUpdated(bytes32 indexed channelId, uint256 newBalance);
    event ChannelBalanceDecreaseRequested(bytes32 indexed channelId);
    event NewLoan(bytes32 indexed channelId, uint256 loanAmount);
    event MaxLoanValueUpdated(uint256 _newMaxLoan);
    event PromiseSettled(bytes32 indexed channelId, address beneficiary, uint256 amount, uint256 totalSettled);
    event ChannelBeneficiaryChanged(bytes32 channelId, address newBeneficiary);
    event AccountantFeeUpdated(uint16 newFee, uint64 validFromBlock);
    event AccountantClosed(uint256 blockNumber);
    event ChannelOpeningPaused();
    event ChannelOpeningActivated();
    event FundsWithdrawned(uint256 amount, address beneficiary);
    event AccountantStakeIncreased(uint256 newStake);
    event AccountantPunishmentActivated(uint256 activationBlock);
    event AccountantPunishmentDeactivated();
    event NewAccountantOperator(address newOperator);

    modifier onlyOperator() {
        require(msg.sender == operator, "only operator can call this function");
        _;
    }

    /*
      ------------------------------------------- SETUP -------------------------------------------
    */

    // Because of proxy pattern this function is used insted of constructor.
    // Have to be called right after proxy deployment.
    function initialize(address _token, address _operator, uint16 _fee, uint256 _maxLoan) public {
        require(!isInitialized(), "have to be not initialized");
        require(_operator != address(0), "operator have to be set");
        require(_token != address(0), "token can't be deployd into zero address");
        require(_fee <= 5000, "fee can't be bigger than 50%");

        token = IERC20(_token);
        registry = IdentityRegistry(msg.sender);
        operator = _operator;
        lastFee = AccountantFee(_fee, uint64(block.number));
        maxLoan = _maxLoan;
        stake = token.balanceOf(address(this));
    }

    function isInitialized() public view returns (bool) {
        return operator != address(0);
    }

    /*
      -------------------------------------- MAIN FUNCTIONALITY -----------------------------------
    */

    // Open incomming payments (also known as provider) channel.
    function openChannel(address _party, address _beneficiary, uint256 _amountToLend) public {
        require(msg.sender == address(registry), "only registry can open channels");
        require(getStatus() == Status.Active, "accountant have to be in active state");

        // channel ID is keccak(identityHash, accountantID)
        bytes32 _channelId = keccak256(abi.encodePacked(_party, address(this)));
        require(!isOpened(_channelId), "channel have to be not opened yet");

        channels[_channelId].beneficiary = _beneficiary;
        channels[_channelId].balance = _amountToLend;

        // During opening new channel user can lend some funds to be guaranteed on channels size
        if (_amountToLend > 0) {
            require(_amountToLend <= maxLoan, "amount to lend can't be bigger that maximally allowed");
            require(token.transferFrom(msg.sender, address(this), _amountToLend), "token transfer should succeed");

            lockedFunds = lockedFunds.add(_amountToLend);
            channels[_channelId].loan = _amountToLend;
            totalLoan = totalLoan.add(_amountToLend);

            emit NewLoan(_channelId, _amountToLend);
        }

        emit ChannelOpened(_channelId, _amountToLend);
    }

    // Settle promise
    // _lock is random number generated by receiver used in HTLC
    function settlePromise(bytes32 _channelId, uint256 _amount, uint256 _transactorFee, bytes32 _lock, bytes memory _signature) public {
        Channel storage _channel = channels[_channelId];
        require(_channel.beneficiary != address(0), "channel should exist");

        bytes32 _hashlock = keccak256(abi.encodePacked(_lock));
        address _signer = keccak256(abi.encodePacked(_channelId, _amount, _transactorFee, _hashlock)).recover(_signature);
        require(_signer == operator, "have to be signed by channel operator");

        // Calculate amount of tokens to be claimed.
        uint256 _unpaidAmount = _amount.sub(_channel.settled);
        require(_unpaidAmount > 0, "amount to settle should be greater that already settled");

        // If signer has less tokens than asked to transfer, we can transfer as much as he has already
        // and rest tokens can be transferred via same promise but in another tx 
        // when signer will top up channel balance.
        uint256 _currentBalance = _channel.balance;
        if (_unpaidAmount > _currentBalance) {
            _unpaidAmount = _currentBalance;
        }

        // Increase already paid amount
        _channel.settled = _channel.settled.add(_unpaidAmount);

        // Calculate accountant fee
        uint256 _accountantFee = calculateAccountantFee(_unpaidAmount);

        // Transfer tokens and decrease balance
        token.transfer(_channel.beneficiary, _unpaidAmount.sub(_transactorFee).sub(_accountantFee));
        _channel.balance = _currentBalance.sub(_unpaidAmount);
        lockedFunds = lockedFunds.sub(_unpaidAmount);

        // Pay fee
        if (_transactorFee > 0) {
            token.transfer(msg.sender, _transactorFee);
        }

        emit PromiseSettled(_channelId, _channel.beneficiary, _unpaidAmount, _channel.settled);
    }

    // Accountant can update channel balance by himself. He can update into any amount size 
    // but not less that provider's loan amount.
    function updateChannelBalance(bytes32 _channelId, uint256 _newBalance) public onlyOperator {
        require(isAccountantActive(), "accountant have to be active");
        require(isOpened(_channelId), "channel have to be opened");
        require(_newBalance >= channels[_channelId].loan, "balance can't be less than loan amount");

        Channel storage _channel = channels[_channelId];
        uint256 _diff;

        if (_newBalance > _channel.balance) {
            _diff = _newBalance.sub(_channel.balance);
            require(availableBalance() >= _diff, "should be enough available balance");
            lockedFunds = lockedFunds.add(_diff);
        } else {
            // If timelock is 0 then we should enable waiting period
            if (_channel.timelock == 0) {
                _channel.timelock = getTimelock();
                emit ChannelBalanceDecreaseRequested(_channelId);
                return;
            }

            // It's still waiting period, do nothing
            if (block.number < _channel.timelock) {
                return;
            }

            _diff = _channel.balance.sub(_newBalance);
            lockedFunds = lockedFunds.sub(_diff);
            _channel.timelock = 0;
        }

        _channel.balance = _newBalance;

        emit ChannelBalanceUpdated(_channelId, _channel.balance);
    }

    // Possibility to increase channel ballance without operator's signature (up to lended amount)
    function rebalanceChannel(bytes32 _channelId) public {
        require(isAccountantActive(), "accountant have to be active");

        Channel storage _channel = channels[_channelId];
        require(_channel.loan > _channel.balance, "new balance should be bigger that current");

        uint256 _increaseAmount = _channel.loan.sub(_channel.balance);

        // If there are not enought funds to rebalance we have to enable punishment mode and rebalance into max possible amount.
        uint256 _minimalExpectedBalance = minimalExpectedBalance().add(_increaseAmount);
        uint256 _currentBalance = token.balanceOf(address(this));
        if (_currentBalance < _minimalExpectedBalance) {
            status = Status.Punishment;
            punishment.activationBlock = block.number;
            _increaseAmount = _minimalExpectedBalance.sub(_currentBalance);
            emit AccountantPunishmentActivated(block.number);
        }

        lockedFunds = lockedFunds.add(_increaseAmount);
        _channel.balance = _channel.balance.add(_increaseAmount);

        emit ChannelBalanceUpdated(_channelId, _channel.balance);
    }

    // Accountant's available funds withdrawal. Can be done only if chanel is not closed and not in punishment mode.
    // Accountant can't withdraw stake, locked in channel funds and funds lended to him.
    function withdraw(address _beneficiary, uint256 _amount) public onlyOperator {
        require(isAccountantActive(), "accountant have to be active");
        require(availableBalance() >= _amount, "should be enough funds available to withdraw");

        token.transfer(_beneficiary, _amount);

        emit FundsWithdrawned(_amount, _beneficiary);
    }

    /*
      -------------------------------------- LOAN MANAGEMENT --------------------------------------
    */

    // Anyone can increase channel's capacity by lending more for accountant
    function increaseLoan(bytes32 _channelId, uint256 _amount) public {
        require(isOpened(_channelId), "channel have to be opened");
        require(getStatus() != Status.Closed, "accountant should be not closed");

        Channel storage _channel = channels[_channelId];

        uint256 _newLoanAmount = _channel.loan.add(_amount);
        require(_newLoanAmount <= maxLoan, "amount to lend can't be bigger that maximally allowed");

        // TODO Transfer from consumer channel instead of msg.sender
        require(token.transferFrom(msg.sender, address(this), _amount), "transfer have to be successfull");

        lockedFunds = lockedFunds.add(_newLoanAmount.sub(_channel.balance));
        totalLoan = totalLoan.add(_amount);
        _channel.balance = _newLoanAmount;
        _channel.loan = _newLoanAmount;

        emit ChannelBalanceUpdated(_channelId, _newLoanAmount);
        emit NewLoan(_channelId, _amount);
    }

    // Withdraw part of loan. This will also decrease channel balance. 
    function decreaseLoan(bytes32 _channelId, uint256 _amount, uint256 _nonce, bytes memory _signature) public {
        address _signer = keccak256(abi.encodePacked(LOAN_RETURN_PREFIX, _channelId, _amount, _nonce)).recover(_signature);
        require(getChannelId(_signer) == _channelId, "have to be signed by channel party");

        require(isOpened(_channelId), "channel have to be opened");
        Channel storage _channel = channels[_channelId];

        require(_nonce > _channel.lastUsedNonce, "nonce have to be bigger than already used");
        _channel.lastUsedNonce = _nonce;

        require(_amount <= _channel.loan, "can't withdraw more than lended");

        uint256 _channelBalanceDiff = min(_channel.balance, _amount);

        // Enable punishment mode when accountnant token amount after loan decrease if less than minimal expected.
        uint256 _minimalExpectedBalance = minimalExpectedBalance().sub(_channelBalanceDiff);
        uint256 _currentBalance = token.balanceOf(address(this));
        if (_amount > _currentBalance || _currentBalance.sub(_amount) < _minimalExpectedBalance) {
            if (isAccountantActive()) {
                status = Status.Punishment;
                punishment.activationBlock = block.number;
                emit AccountantPunishmentActivated(block.number);
            }
            _amount = _currentBalance.sub(_minimalExpectedBalance);
        }

        uint256 _newLoanAmount = _channel.loan.sub(_amount);
        require(_newLoanAmount <= maxLoan, "amount to lend can't be bigger that maximally allowed");

        token.transfer(_channel.beneficiary, _amount);

        _channel.loan = _newLoanAmount;
        _channel.balance = _channel.balance.sub(_channelBalanceDiff);
        lockedFunds = lockedFunds.sub(_channelBalanceDiff);
        totalLoan = totalLoan.sub(_amount);

        emit ChannelBalanceUpdated(_channelId, _channel.balance);
        emit NewLoan(_channelId, _newLoanAmount);
    }

    /*
      ------------------------------------------ HELPERS ------------------------------------------
    */

    function resolveEmergency() public {
        require(getStatus() == Status.Punishment, "accountant should be in punishment status");

        // 0.04% of total channels amount per unit
        uint256 _punishmentPerUnit = round(lockedFunds.mul(4), 100).div(100);

        // No punishment during first unit.
        uint256 _unit = getUnitBlocks();
        uint256 _blocksPassed = block.number - punishment.activationBlock;
        uint256 _punishmentUnits = (round(_blocksPassed, _unit) / _unit).sub(1);

        uint256 _punishmentAmount = _punishmentUnits.mul(_punishmentPerUnit);
        punishment.amount = punishment.amount.add(_punishmentAmount);

        uint256 _shouldHave = max(lockedFunds, totalLoan).add(max(stake, punishment.amount));
        uint256 _currentBalance = token.balanceOf(address(this));
        uint256 _missingFunds = (_currentBalance < _shouldHave) ? _shouldHave.sub(_currentBalance) : uint256(0);

        // If there are not enough available funds, they have to be topuped from msg.sender.
        token.transferFrom(msg.sender, address(this), _missingFunds);

        // Disable punishment mode
        status = Status.Active;

        emit AccountantPunishmentDeactivated();
    }

    function setBeneficiary(bytes32 _channelId, address _newBeneficiary, uint256 _nonce, bytes memory _signature) public {
        require(isOpened(_channelId), "channel have to be opened");
        require(_newBeneficiary != address(0), "beneficiary can't be zero address");
        Channel storage _channel = channels[_channelId];
        require(_nonce > _channel.lastUsedNonce, "nonce have to be bigger than already used");

        address _signer = keccak256(abi.encodePacked(_channelId, _newBeneficiary, _nonce)).recover(_signature);
        require(getChannelId(_signer) == _channelId, "have to be signed by channel party");

        _channel.lastUsedNonce = _nonce;
        _channel.beneficiary = _newBeneficiary;

        emit ChannelBeneficiaryChanged(_channelId, _newBeneficiary);
    }

    function setAccountantOperator(address _newOperator) public onlyOperator {
        require(_newOperator != address(0), "can't be zero address");
        operator = _newOperator;
        emit NewAccountantOperator(_newOperator);
    }

    function setMaxLoan(uint256 _newMaxLoan) public onlyOperator {
        require(isAccountantActive(), "accountant have to be active");
        maxLoan = _newMaxLoan;
        emit MaxLoanValueUpdated(_newMaxLoan);
    }

    function setAccountantFee(uint16 _newFee) public onlyOperator {
        require(getStatus() != Status.Closed, "accountant should be not closed");
        require(_newFee <= 5000, "fee can't be bigger that 50%");
        require(block.number >= lastFee.validFrom, "can't update inactive fee");

        // new fee will start be valid after delay block will pass
        uint64 _validFrom = uint64(getTimelock());

        previousFee = lastFee;
        lastFee = AccountantFee(_newFee, _validFrom);

        emit AccountantFeeUpdated(_newFee, _validFrom);
    }

    function calculateAccountantFee(uint256 _amount) public view returns (uint256) {
        AccountantFee memory _activeFee = (block.number >= lastFee.validFrom) ? lastFee : previousFee;
        return round((_amount * uint256(_activeFee.value) / 100), 100) / 100;
    }

    function increaseAccountantStake(uint256 _additionalStake) public onlyOperator {
        if (availableBalance() < _additionalStake) {
            uint256 _diff = _additionalStake.sub(availableBalance());
            token.transferFrom(msg.sender, address(this), _diff);
        }

        stake = stake.add(_additionalStake);

        emit AccountantStakeIncreased(stake);
    }

    function isOpened(bytes32 _channelId) public view returns (bool) {
        return channels[_channelId].beneficiary != address(0);
    }

    // If Accountant is not closed and is not in punishment mode, he is active.
    function isAccountantActive() public view returns (bool) {
        Status _status = getStatus();
        return _status != Status.Punishment && _status != Status.Closed;
    }

    function pauseChannelOpening() public onlyOperator {
        require(getStatus() == Status.Active, "accountant have to be in active state");
        status = Status.Paused;
        emit ChannelOpeningPaused();
    } 

    function activateChannelOpening() public onlyOperator {
        require(getStatus() == Status.Paused, "accountant have to be in paused state");
        status = Status.Active;
        emit ChannelOpeningActivated();
    }

    // Returns funds amount not locked in any channel, not staked and not lended from providers.
    function availableBalance() public view returns (uint256) {
        uint256 _totalLockedAmount = max(lockedFunds, totalLoan).add(max(stake, punishment.amount));
        if (_totalLockedAmount > token.balanceOf(address(this))) {
            return uint256(0);
        }
        return token.balanceOf(address(this)).sub(_totalLockedAmount);
    }

    // Funds which always have to be holded in accountant smart contract.
    function minimalExpectedBalance() public view returns (uint256) {
        return max(stake, punishment.amount).add(lockedFunds); 
        // return max(lockedFunds, totalLoan).add(max(stake, punishment.amount))
    }

    function closeAccountant() public onlyOperator {
        require(isAccountantActive(), "accountant should be active");
        status = Status.Closed;
        closingTimelock = getEmergencyTimelock();
        emit AccountantClosed(block.number);
    }

    function getStakeBack(address _beneficiary) public onlyOperator {
        require(getStatus() == Status.Closed, "accountant have to be closed");
        require(block.number > closingTimelock, "timelock period have be already passed");

        uint256 _amount = token.balanceOf(address(this)).sub(punishment.amount);
        token.transfer(_beneficiary, _amount);
    }

    function getUnitBlocks() internal pure returns (uint256) {
        return UNIT_BLOCKS;
    }

    // Returns blocknumber until which exit request should be locked
    function getTimelock() internal view returns (uint256) {
        return block.number + DELAY_BLOCKS;
    }

    function getEmergencyTimelock() internal view returns (uint256) {
        return block.number + DELAY_BLOCKS * 100; // +/- 300 days
    }

    function max(uint a, uint b) private pure returns (uint) {
        return a > b ? a : b;
    }

    function min(uint a, uint b) private pure returns (uint) {
        return a < b ? a : b;
    }

    function round(uint a, uint m) private pure returns (uint ) {
        return ((a + m - 1) / m) * m;
    }

    // Setting new destination of funds recovery.
    function setFundsDestination(address payable _newDestination) public onlyOperator {
        require(_newDestination != address(0));
        emit DestinationChanged(fundsDestination, _newDestination);
        fundsDestination = _newDestination;
    }

}
